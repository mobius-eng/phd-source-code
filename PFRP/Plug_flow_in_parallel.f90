MODULE PLUG_FLOW_IN_PARALLEL

USE ISO_C_BINDING, ONLY : C_DOUBLE, C_FLOAT, C_INT, C_FUNPTR, C_F_PROCPOINTER
USE MPFP, ONLY: MPFP_RUN => RUN, MASS_BALANCE
USE NEWTON, ONLY: FUN_WITH_DERIV
USE ENV, ONLY: C_REAL
USE CSTR

IMPLICIT NONE

CONTAINS

SUBROUTINE RUN_SIMULATION_GENERAL(C, X, MBERR, ALPHA, FN, LMAX, TMAX, TOL, &
        MAXITER, GSITER, NEWTITER, ERR, N, M) BIND(C, NAME="run_simulation_general")

!DIR$ ATTRIBUTES DLLEXPORT :: RUN_SIMULATION_GENERAL

REAL(C_REAL), DIMENSION(0:N, 0:M), INTENT(INOUT) :: C, X
REAL(C_REAL), DIMENSION(5) :: MBERR
REAL(C_REAL), INTENT(IN) :: ALPHA, LMAX, TMAX, TOL
INTEGER(C_INT), INTENT(IN) :: MAXITER, N, M
REAL(C_REAL), INTENT(INOUT) :: ERR
INTEGER(C_INT), INTENT(OUT) :: GSITER, NEWTITER
TYPE(C_FUNPTR) :: FN

PROCEDURE(FUN_WITH_DERIV), POINTER :: PFN
REAL(C_REAL) :: H, K, R

H = LMAX / N
K = TMAX / M
R = K / H

CALL C_F_PROCPOINTER(FN, PFN)

CALL MPFP_RUN(C, X, ALPHA, PFN, K, R, TOL, MAXITER, N, M, GSITER, NEWTITER, ERR)
CALL MASS_BALANCE(C, X, PFN, K, R, MBERR(1), MBERR(2), MBERR(3), MBERR(4), MBERR(5), N, M)

END SUBROUTINE RUN_SIMULATION_GENERAL


SUBROUTINE RUN_SIMULATION_WITH_SIMPLE_RATE( &
    REACTOR_PARAMS, LMAX, TMAX, TOL, MAXITER, CX, MBERR, GSITER, NEWTITER, ERR, N, M) &
    BIND(C, NAME="run_simulation_with_simple_rate")
    
!DIR$ ATTRIBUTES DLLEXPORT :: RUN_SIMULATION_WITH_SIMPLE_RATE

REAL(C_REAL), DIMENSION(0:N, 0:M, 1:2), INTENT(INOUT) :: CX
REAL(C_REAL), DIMENSION(5), INTENT(OUT) :: MBERR
REAL(C_REAL), DIMENSION(1:4) :: REACTOR_PARAMS
REAL(C_REAL), INTENT(IN) :: LMAX, TMAX, TOL
INTEGER(C_INT), INTENT(IN) :: MAXITER, N, M
REAL(C_REAL), INTENT(INOUT) :: ERR
INTEGER(C_INT), INTENT(OUT) :: GSITER, NEWTITER

REAL(C_REAL) :: H, K, R
REAL(C_REAL) :: ALPHA, BETA, PHI, K0

ALPHA = REACTOR_PARAMS(1)
BETA  = REACTOR_PARAMS(2)
PHI   = REACTOR_PARAMS(3)
K0    = REACTOR_PARAMS(4)


H = LMAX / N
K = TMAX / M
R = K / H

CALL MPFP_RUN(CX(:,:,1), CX(:,:,2), ALPHA, SIMPLE_RATE, K, R, TOL, MAXITER, N, M, GSITER, NEWTITER, ERR)
CALL MASS_BALANCE(CX(:,:,1), CX(:,:,2), SIMPLE_RATE, K, R, MBERR(1), MBERR(2), MBERR(3), MBERR(4), MBERR(5), N, M)


    CONTAINS
    
    PURE SUBROUTINE SIMPLE_RATE(C, X, F, FC, FX)
    
    REAL(C_DOUBLE), INTENT(IN) :: C, X
    REAL(C_DOUBLE), INTENT(OUT) :: F, FC, FX

    IF (C <= 0 .OR. X >= 0.999) THEN
        F = 0.0_C_DOUBLE
        FC = F
        FX = F
        RETURN
    END IF

    F = K0 * (C ** (BETA - 1)) * (1 - X) ** (PHI - 1)

    FC = F * BETA * (1 - X)
    FX = -PHI * F * C
    F = F * C * (1 - X)

    END SUBROUTINE SIMPLE_RATE

END SUBROUTINE RUN_SIMULATION_WITH_SIMPLE_RATE


SUBROUTINE RUN_CSTR_WITH_SIMPLE_RATE(CFLOW, CREACT, C_INLET, K0, BETA, PHI, DT, C, X, NTIME) &
        BIND(C, NAME="run_cstr_with_simple_rate")

!DIR$ ATTRIBUTES DLLEXPORT :: RUN_CSTR_WITH_SIMPLE_RATE

REAL(C_REAL), INTENT(IN) :: CFLOW, CREACT, C_INLET, K0, BETA, PHI, DT
REAL(C_REAL), DIMENSION(0:NTIME), INTENT(INOUT) :: C, X
INTEGER(C_INT), INTENT(IN) :: NTIME

TYPE(CSTR_CONFIG) :: CONF
TYPE(CSTR_STATE), DIMENSION(0:NTIME) :: STATES

INTEGER(C_INT) :: I

CONF%CFLOW = CFLOW; CONF%CREACT = CREACT;
CONF%C_INLET => CIN
CONF%SOURCE_TERM => SIMPLE_RATE

STATES(0)%C = C(0)
STATES(0)%X = X(0)

CALL RUN_CSTR_SIMULATION(CONF, 0.0_C_REAL, DT, STATES, 1.0E-8_C_REAL, 1.0E-8_C_REAL, 20) 

DO I=1,NTIME
    C(I) = STATES(I)%C
    X(I) = STATES(I)%X
END DO

    CONTAINS
    
    PURE FUNCTION CIN(T)
    REAL(C_REAL), INTENT(IN) :: T
    REAL(C_REAL) :: CIN
    
    CIN = C_INLET
    END FUNCTION CIN
    
    
    SUBROUTINE SIMPLE_RATE(STATE, F, FC, FX)
    
    TYPE(CSTR_STATE), INTENT(IN) :: STATE
    REAL(C_DOUBLE), INTENT(OUT) :: F, FC, FX
    
    REAL(C_DOUBLE) :: C, X

    C = STATE%C; X = STATE%X
    
    IF (C <= 0 .OR. X >= 0.999) THEN
        F = 0.0_C_DOUBLE
        FC = F
        FX = F
        RETURN
    END IF

    F = K0 * (C ** (BETA - 1)) * (1 - X) ** (PHI - 1)

    FC = F * BETA * (1 - X)
    FX = -PHI * F * C
    F = F * C * (1 - X)
    END SUBROUTINE SIMPLE_RATE
    
END SUBROUTINE RUN_CSTR_WITH_SIMPLE_RATE

    
END MODULE
    